# stormpath-vb-console-sample
Original post: [A Guide to Easy Authentication and Authorization in Visual Basic.NET](https://stormpath.com/blog/easy-authentication-and-authorization-in-visual-basic-dotnet/)

---

What's that, you say? You need an easy way to handle user identity management and authorization, in just a few lines of code? You're in luck! The [Stormpath .NET SDK](https://github.com/stormpath/stormpath-sdk-dotnet) was built from the ground up with first-class support for VB.NET in mind.

In this short guide, I'll walk through the steps necessary to build a simple VB.NET console application that uses Stormpath for [user authentication and authorization](https://stormpath.com/product/).

To follow along, create a new Visual Basic.NET console project in Visual Studio. If you're using a Mac, check out the guide I wrote about how I turned a MacBook Pro into the ultimate [Visual Studio development machine](https://stormpath.com/blog/ultimate-guide-to-using-visual-studio-on-a-mac/).

### Getting an API Key

All API requests to Stormpath require a valid API Key and Secret. The Stormpath .NET SDK does this automatically; all you have to do is generate an API Key in the Stormpath Admin Console.

1. If you haven't already, [register for Stormpath](https://api.stormpath.com/register), and click the link in the verification email.
2. Log in to the [Stormpath Admin Console](https://api.stormpath.com).
3. Click **Create API Key** or **Manage API Keys** under Developer Tools on the right side of the page.
4. Scroll down to Security Credentials and click **Create API Key**. This will generate your API Key and download it to your computer as an `apiKey.properties` file.

We recommend you store the API Key and Secret values as environment variables, or place the downloaded file into a hidden folder in your home directory. (In a production environment, environment variables are highly preferred!)

* To store the API Key and Secret as secure environment variables, open the `apiKey.properties` file in a text editor and execute these commands in a terminal window (Command Prompt or PowerShell both work):

```
setx STORMPATH_API_KEY_ID "[value-from-properties-file]"
setx STORMPATH_API_KEY_SECRET "[value-from-properties-file]"
```

* To store the file in a hidden home directory folder, execute these commands instead:


```
cd [folder-you-downloaded-the-file-to]
mkdir %homedrive%%homepath%\.stormpath
copy apiKey.properties %homedrive%%homepath%\.stormpath\
```

Or, in PowerShell:

```
cd [folder-you-downloaded-the-file-to]
mkdir ~\.stormpath
copy apiKey.properties ~\.stormpath\
```

### Installing the .NET SDK

Use the Package Manager GUI or the console to install the [Stormpath.SDK](https://www.nuget.org/packages/Stormpath.SDK) package:

    install-package Stormpath.SDK

All the dependent packages are pulled in for you automatically.

### Creating a Client and Connecting to Stormpath

Paste this code into the `Module1.vb` file generated by Visual Studio:

``` vbnet
Option Strict On
Option Explicit On
Option Infer On
Imports Stormpath.SDK
Imports Stormpath.SDK.Account
Imports Stormpath.SDK.Api
Imports Stormpath.SDK.Client
Imports Stormpath.SDK.Error
Imports Stormpath.SDK.Group

Sub Main()
    RunDemo()
End Sub

Private Sub RunDemo()
    ' Your code goes here
End Sub
```

Next, point the Stormpath SDK to the location of the `apiKey.properties` file:

``` vbnet
Dim apiKey = ClientApiKeys.Builder _
    .SetFileLocation("[path-to-file]") _
    .Build()
```

> Note: If you followed the steps above to create secure environment variables (or placed the file into the default location of `~\.stormpath\apiKey.properties`), you can omit this step! The next call to `Clients.Builder.Build()` will check the default locations for you; creating an `IClientApiKey` instance is only necessary if you need to specify the location of the API Key file manually.

Create an `IClient` instance. You'll reuse this object to make calls to the Stormpath API:

``` vbnet
' Call to .SetApiKey() is only necessary if you manually created an IClientApiKey above
Dim client = Clients.Builder _
    .SetApiKey(apiKey) _
    .Build()
```

When you registered for Stormpath, a default application called "My Application" was created for you. You'll need to retrieve it by making a request to the Stormpath API:

``` vbnet
Dim app = Await client.GetApplications _
    .Where(Function(a) a.Name = "My Application") _
    .FirstAsync()
```

### Asynchronous and Synchronous Programming in VB.NET

This tutorial uses the `Await`able asynchronous methods available in the SDK. If you're following along, you'll need to make a small change:

``` vbnet

Sub Main()
    RunDemo.GetAwaiter.GetResult()
End Sub

Private Async Function RunDemo() As Task
    ' ... your existing code here
End Function

```

The SDK includes a synchronous API as well, if you don't want to use `Async` and `Await` in your code. To make synchronous API calls, use the methods in the `Stormpath.SDK.Sync` namespace instead, and use the `Synchronously` helper method in LINQ queries:

``` vbnet
Imports Stormpath.SDK.Sync

Dim app = client.GetApplications _
    .Synchronously()
    .Where(Function(a) a.Name = "My Application") _
    .First()
```

### Storing User Profiles

Creating a user account is straightforward:

``` vbnet
Dim joe = client.Instantiate(Of IAccount) _
    .SetGivenName("Joe") _
    .SetSurname("Stormtrooper") _
    .SetEmail("tk421@deathstar.co") _
    .SetPassword("Changeme!123")
```

To persist the account in Stormpath, you simply `Await CreateAccountAsync` (or call `CreateAccount` if you are using the Sync namespace):
``` vbnet
Await app.CreateAccountAsync(joe)
```

### Authenticating a User

To make a login request, call `AuthenticateAccountAsync` and pass the username/email and password. This method will throw a `ResourceException` if the login is invalid, so you can wrap it in a try/catch block:

``` vbnet
Try
    Dim loginResult = Await app.AuthenticateAccountAsync("tk421@deathstar.co", "Changeme!123")
    Dim loginAccount = Await loginResult.GetAccountAsync()
    Console.WriteLine("User " & loginAccount.FullName & " logged in!")
Catch rex As ResourceException
    Console.WriteLine("Could not log in. Error: " & rex.Message)
End Try
```

If the login attempt is successful, the returned `IAuthenticationResult` instance will include a link to the authenticated account. Calling `GetAccountAsync` will follow the link and retrieve the account details.

### Role-Based Authorization Using Groups

Stormpath [models roles using Groups](https://stormpath.com/product/authorization/). To demonstrate assigning a role to Joe Stormtrooper, create some demo groups:

```vbnet
' In a production application, these would be created beforehand and only once
Dim demoUsers = client.Instantiate(Of IGroup) _
    .SetName("DemoUsers") _
    .SetDescription("Demo users who do not have administrator access.")
Dim demoAdmins = client.Instantiate(Of IGroup) _
    .SetName("DemoAdmins") _
    .SetDescription("Demo users who have administrator access.")

Await Task.WhenAll(
    app.CreateGroupAsync(demoUsers),
    app.CreateGroupAsync(demoAdmins))
```

Once the Groups are set up, assigning a user a role is a single line of code!

``` vbnet
Await joe.AddGroupAsync(demoUsers)
```

When your application needs to perform an authorization check, the Groups a user belongs to can be enumerated, and your application can grant or restrict access based on which Groups are present.

As a simple example, you can print the Groups Joe belongs to:

``` vbnet
Dim roleNames = (Await joe.GetGroups().ToListAsync()) _
    .Select(Function(g) g.Name)
Console.WriteLine("Roles for " & joe.GivenName & ": " &
                  String.Join(", ", roles))
```

### Fine-Grained Permissions with Custom Data

If you need more granular control of a user's permissions, you can store arbitrary key-value pairs using [Custom Data](http://docs.stormpath.com/rest/product-guide/#custom-data).

For example, to indicate that Joe has read (but not write) access, you could store these values:

``` vbnet
joe.CustomData.Put(New With {.read = True, .write = False})
Await joe.SaveAsync()
```

Checking the user's permissions becomes a simple matter of getting and parsing the values you stored earlier:

``` vbnet
Dim joeCustomData = Await joe.GetCustomDataAsync()
Dim canRead = CBool(joeCustomData("read"))
Dim canWrite = CBool(joeCustomData("write"))
Console.WriteLine("Can Joe read? " & canRead)
Console.WriteLine("Can Joe write? " & canWrite)
```

Custom Data isn't limited to storing permissions data; it can also be used to store additional user profile information, or any other schemaless data you need to persist alongside your Stormpath resources. You can store strings, booleans, and other primitive data types - up to 10MB of data per resource.

### Next Steps

* Read up on the [documentation](https://docs.stormpath.com/)
* Browse the [SDK source](https://github.com/stormpath/stormpath-sdk-dotnet)
* Join [the conversation](https://github.com/stormpath/stormpath-sdk-dotnet/issues) and let us know what you want to see!
